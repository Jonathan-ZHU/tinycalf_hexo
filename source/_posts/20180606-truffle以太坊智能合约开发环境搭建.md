---
title: truffle以太坊智能合约开发环境搭建
date: 2018-06-27 18:10:37
tags:
- solidity
- ethereum
categories: 区块链
author: TinyCalf
---

> truffle 是以太坊智能合约Solidity编程的IDE之一。 它能在没有搭建geth/parity等以太坊节点的情况下发布并调试合约。在truffle中可以用传统的nodejs测试模块为合约编写测试脚本，并且帮助你省去了 "发布合约-调试合约-重新发布合约" 的麻烦。本篇旨在说明truffle最核心的操作流程，帮助快速上手使用。

## 安装 npm nodejs
我这以前有一篇快速安装教程， [看这里](https://www.tiny-calf.com/2017/07/20/Node.js%E5%BF%AB%E9%80%9F%E5%AE%89%E8%A3%85/)


## 安装truffle
```
npm install -g truffle
```
<!-- more -->
## 新建truffle项目
官网关于这部分的详细说明在[这里](https://truffleframework.com/docs/getting_started/project)
我们可以直接创建项目：
```
mkdir MetaCoin
cd MetaCoin
truffle unbox metacoin
```
metacoin是truffle已经存在的一个box，你也可以新建其他box，box其实就是一个可以直接发布的合约模板，包含了项目的源码和测试脚本等文件.你也可以创建一个空的项目：
```
cd path/to/your/empty/dir
truffle init
```
这里包括下面为了说明truffle各个功能，我创建了一个名叫TinyCoin的项目，里面会写一个标准的ERC20合约：
```
mkdir TinyCoin
cd TinyCoin
truffle init
```
完成以后目录会有几文件夹，分别是：
* contracts 存放合约solidity源码
* migrates  存放js写的迁移脚本,帮助你发布合约
* test 存放js写的测试脚本

## 写一个智能合约
这里我在contracts文件夹下面写了一个标准的ERC20合约,文件名为TinyCoin.sol：
```
pragma solidity ^0.4.18;

interface tokenRecipient { function receiveApproval (address _from, uint256 _value, address _token, bytes _extraData) external; }

contract TinyCoin {
    // Public variables of the token
    string public name;
    string public symbol;
    uint8 public decimals = 18;
    // 18 decimals is the strongly suggested default, avoid changing it
    uint256 public totalSupply;

    // This creates an array with all balances
    mapping (address => uint256) public balanceOf;
    mapping (address => mapping (address => uint256)) public allowance;

    // This generates a public event on the blockchain that will notify clients
    event Transfer(address indexed from, address indexed to, uint256 value);

    // This notifies clients about the amount burnt
    event Burn(address indexed from, uint256 value);

    /**
     * Constrctor function
     *
     * Initializes contract with initial supply tokens to the creator of the contract
     */
    constructor(
        uint256 initialSupply,
        string tokenName,
        string tokenSymbol
    ) public {
        totalSupply = initialSupply * 10 ** uint256(decimals);  // Update total supply with the decimal amount
        balanceOf[msg.sender] = totalSupply;                // Give the creator all initial tokens
        name = tokenName;                                   // Set the name for display purposes
        symbol = tokenSymbol;                               // Set the symbol for display purposes
    }

    /**
     * Internal transfer, only can be called by this contract
     */
    function _transfer(address _from, address _to, uint _value) internal {
        // Prevent transfer to 0x0 address. Use burn() instead
        require(_to != 0x0);
        // Check if the sender has enough
        require(balanceOf[_from] >= _value);
        // Check for overflows
        require(balanceOf[_to] + _value > balanceOf[_to]);
        // Save this for an assertion in the future
        uint previousBalances = balanceOf[_from] + balanceOf[_to];
        // Subtract from the sender
        balanceOf[_from] -= _value;
        // Add the same to the recipient
        balanceOf[_to] += _value;
        emit Transfer(_from, _to, _value);
        // Asserts are used to use static analysis to find bugs in your code. They should never fail
        assert(balanceOf[_from] + balanceOf[_to] == previousBalances);
    }

    /**
     * Transfer tokens
     *
     * Send `_value` tokens to `_to` from your account
     *
     * @param _to The address of the recipient
     * @param _value the amount to send
     */
    function transfer(address _to, uint256 _value) public {
        _transfer(msg.sender, _to, _value);
    }

    /**
     * Transfer tokens from other address
     *
     * Send `_value` tokens to `_to` in behalf of `_from`
     *
     * @param _from The address of the sender
     * @param _to The address of the recipient
     * @param _value the amount to send
     */
    function transferFrom(address _from, address _to, uint256 _value) public returns (bool success) {
        require(_value <= allowance[_from][msg.sender]);     // Check allowance
        allowance[_from][msg.sender] -= _value;
        _transfer(_from, _to, _value);
        return true;
    }

    /**
     * Set allowance for other address
     *
     * Allows `_spender` to spend no more than `_value` tokens in your behalf
     *
     * @param _spender The address authorized to spend
     * @param _value the max amount they can spend
     */
    function approve(address _spender, uint256 _value) public
        returns (bool success) {
        allowance[msg.sender][_spender] = _value;
        return true;
    }

    /**
     * Set allowance for other address and notify
     *
     * Allows `_spender` to spend no more than `_value` tokens in your behalf, and then ping the contract about it
     *
     * @param _spender The address authorized to spend
     * @param _value the max amount they can spend
     * @param _extraData some extra information to send to the approved contract
     */
    function approveAndCall(address _spender, uint256 _value, bytes _extraData)
        public
        returns (bool success) {
        tokenRecipient spender = tokenRecipient(_spender);
        if (approve(_spender, _value)) {
            spender.receiveApproval(msg.sender, _value, this, _extraData);
            return true;
        }
    }

    /**
     * Destroy tokens
     *
     * Remove `_value` tokens from the system irreversibly
     *
     * @param _value the amount of money to burn
     */
    function burn(uint256 _value) public returns (bool success) {
        require(balanceOf[msg.sender] >= _value);   // Check if the sender has enough
        balanceOf[msg.sender] -= _value;            // Subtract from the sender
        totalSupply -= _value;                      // Updates totalSupply
        emit Burn(msg.sender, _value);
        return true;
    }

    /**
     * Destroy tokens from other account
     *
     * Remove `_value` tokens from the system irreversibly on behalf of `_from`.
     *
     * @param _from the address of the sender
     * @param _value the amount of money to burn
     */
    function burnFrom(address _from, uint256 _value) public returns (bool success) {
        require(balanceOf[_from] >= _value);                // Check if the targeted balance is enough
        require(_value <= allowance[_from][msg.sender]);    // Check allowance
        balanceOf[_from] -= _value;                         // Subtract from the targeted balance
        allowance[_from][msg.sender] -= _value;             // Subtract from the sender's allowance
        totalSupply -= _value;                              // Update totalSupply
        emit Burn(_from, _value);
        return true;
    }
}

```
另外在这个文件夹下面还自带一个migration的合约，是truffle用来做迁移用的，可以暂时不用管

## 迁移脚本
这个内容在官网[这里](https://truffleframework.com/docs/getting_started/migrations)可以查到。
在migrates文件夹下面新建一个`2_deploy_contracts.js`的文件，里面写上发布合约的内容，内容基本是固定的：
```
var TinyCoin = artifacts.require("./TinyCoin.sol");
module.exports = function(deployer) {
  deployer.deploy(TinyCoin, 10000, "TinyCoin", "TINY");
};
```
如果合约需要传参数，像上面一样把无论多少个参数跟在deploy后面就行。我这里是三个参数。
官网也给出更多发布合约函数的例子:
```
// Deploy a single contract without constructor arguments
deployer.deploy(A);

// Deploy a single contract with constructor arguments
deployer.deploy(A, arg1, arg2, ...);

// Don't deploy this contract if it has already been deployed
deployer.deploy(A, {overwrite: false});

// Set a maximum amount of gas and `from` address for the deployment
deployer.deploy(A, {gas: 4612388, from: "0x...."});

// Deploy multiple contracts, some with arguments and some without.
// This is quicker than writing three `deployer.deploy()` statements as the deployer
// can perform the deployment as a single batched request.
deployer.deploy([
  [A, arg1, arg2, ...],
  B,
  [C, arg1]
]);

// External dependency example:
//
// For this example, our dependency provides an address when we're deploying to the
// live network, but not for any other networks like testing and development.
// When we're deploying to the live network we want it to use that address, but in
// testing and development we need to deploy a version of our own. Instead of writing
// a bunch of conditionals, we can simply use the `overwrite` key.
deployer.deploy(SomeDependency, {overwrite: false});
```
## 以太坊虚拟机调试环境
### 启动truffle自带的evm
truffle能支持所有evm, 包括geth/parit等等，这里最快最方便的调试方法是truffle自带的evm，你不需要任何安装，并且能完成打包发布等操作,在项目目录下启动：
```
truffle develop
```
然后就能进入命令行界面：
```
Truffle Develop started at http://127.0.0.1:9545/

Accounts:
(0) 0x627306090abab3a6e1400e9345bc60c78a8bef57
(1) 0xf17f52151ebef6c7334fad080c5704d77216b732
(2) 0xc5fdf4076b8f3a5357c5e395ab970b5b54098fef
(3) 0x821aea9a577a9b44299b9c15c88cf3087f3b5544
(4) 0x0d1d4e623d10f9fba5db95830f7d3839406c6af2
(5) 0x2932b7a2355d6fecc4b5c0b6bd44cc31df247a2e
(6) 0x2191ef87e392377ec08e7c08eb105ef5448eced5
(7) 0x0f4f2ac550a1b4e2280d04c21cea7ebd822934b5
(8) 0x6330a553fc93768f612722bb8c2ec78ac90b3bbc
(9) 0x5aeda56215b167893e80b4fe645ba6d5bab767de

Private Keys:
(0) c87509a1c067bbde78beb793e6fa76530b6382a4c0241e5e4a9ec0a0f44dc0d3
(1) ae6ae8e5ccbfb04590405997ee2d52d2b330726137b875053c36d94e974d162f
(2) 0dbbe8e4ae425a6d2687f1a7e3ba17bc98c673636790f1b8ad91193c05875ef1
(3) c88b703fb08cbea894b6aeff5a544fb92e78a18e19814cd85da83b71f772aa6c
(4) 388c684f0ba1ef5017716adb5d21a053ea8e90277d0868337519f97bede61418
(5) 659cbb0e2411a44db63778987b1e22153c086a95eb6b18bdf89de078917abc63
(6) 82d052c865f5763aad42add438569276c00d3d88a2d062d36b2bae914d58b8c8
(7) aa3680d5d48a8283413f7a108367c7299ca73f553735860a87b08f39395618b7
(8) 0f62d96d6675f32685bbdb8ac13cda7c23436f63efbb9d07700d8669ff12b7c4
(9) 8d5366123cb560bb606379f90a0bfd4769eecc0557f1b362dcae9012b548b1e5

Mnemonic: candy maple cake sugar pudding cream honey rich smooth crumble sweet treat

⚠️  Important ⚠️  : This mnemonic was created for you by Truffle. It is not secure.
Ensure you do not use it on production blockchains, or else you risk losing funds.

truffle(develop)>
```
可以看见在本地测试链上新建了很多帐号以供测试，每个帐号都有一些测试币。在命令行下，可以直接使用javascript和 web3.js的语法（用的web3不到1.0的语法，原因可能是web3 1.0有太多promise，不太适合命令行），比如获取钱包余额：
```
truffle(develop)> web3.eth.getBalance("0x627306090abab3a6e1400e9345bc60c78a8bef57")
BigNumber { s: 1, e: 20, c: [ 1000000 ] }
```
### 编译打包智能合约
然后就是最重要的,还是在这个命令行下，我们可以用一个命令编译我们刚才写的ERC20合约：
```
> compile
```
如果合约有error或者warning，都会在这里显示出来。完成编译后，我们可以看到新增了build文件夹,里面有编译后的json文件。正常编译合约会生成两个文件，或者说字符串，一个是abi（二进制接口），另一个是bin（二进制程序），这里的json文件是truffle整合的一个形式，其实里面包含了abi和bin。

### 自动发布合约
ok，接下来我们刚才写的迁移脚本就起到作用了，我们输入命令来执行迁移脚本：
```
truffle(develop)> migrate
Using network 'develop'.

Running migration: 1_initial_migration.js
  Replacing Migrations...
  ... 0x9637b2e62481e718f7ad1279bb27c523744303c82a15f23d1c66e5448e6d877d
  Migrations: 0x8cdaf0cd259887258bc13a92c0a6da92698644c0
Saving successful migration to network...
  ... 0xd7bc86d31bee32fa3988f1c1eabce403a1b5d570340a3a9cdba53a472ee8c956
Saving artifacts...
Running migration: 2_deploy_contracts.js
  Replacing TinyCoin...
  ... 0x0fd1c57e3bee747e5d94568dabd2e1fcd808ca8368f79ddd72238f1d83996948
  TinyCoin: 0x345ca3e014aaf5dca488057592ee47305d9b3e10
Saving successful migration to network...
  ... 0xf36163615f41ef7ed8f4a8f192149a0bf633fe1a2398ce001bf44c43dc7bdda0
Saving artifacts...
```
如果你不是第一个使用migrate，需要加个参数把以前的覆盖掉：
```
> migrate --reset
```
这样合约就运行在本地链上了,合约地址也显示在上面。

### 调试合约
官方文档相关部分点[这里](https://truffleframework.com/docs/getting_started/debugging)。
然后我们用web3的语法发起一个ERC20转账的操作，转账操作会修改合约数据，所以是一个transfer操作（或者叫send操作，相反只查看区块链数据而不需要发起交易的方法叫call）:

```
var contract = web3.eth.contract(TinyCoin.abi)
var contractInstance = contract.at(TinyCoin.address)
contractInstance.transfer(web3.eth.accounts[1],web3.toWei(10), {from:web3.eth.accounts[0]})
```
就输出了一个hash值，这个hash就是调用合约transfer方法的交易的hash值，类似这样：
```
0xe57f3608e5696c5cb18ff2690ad88214150072258dc4cb63375e5d3a47eb9909
```
我们现在有这个交易的hash值了，就可以调试这个已经完成的方法，我们只需要输入：
```
debug 0xe57f3608e5696c5cb18ff2690ad88214150072258dc4cb63375e5d3a47eb9909
```
就能看见调试的交互界面
```
truffle(develop)> debug 0xe57f3608e5696c5cb18ff2690ad88214150072258dc4cb63375e5d3a47eb9909
Compiling ./contracts/Migrations.sol...
Compiling ./contracts/TinyCoin.sol...

Gathering transaction data...

Addresses affected:
 0x345ca3e014aaf5dca488057592ee47305d9b3e10 - TinyCoin

Commands:
(enter) last command entered (step next)
(o) step over, (i) step into, (u) step out, (n) step next
(;) step instruction, (p) print instruction, (h) print this help, (q) quit
(b) toggle breakpoint, (c) continue until breakpoint
(+) add watch expression (`+:<expr>`), (-) remove watch expression (-:<expr>)
(?) list existing watch expressions
(v) print variables and values, (:) evaluate expression - see `v`


TinyCoin.sol:

3: interface tokenRecipient { function receiveApproval (address _from, uint256 _value, address _token, bytes _extraData) external; }
4:
5: contract TinyCoin {
   ^^^^^^^^^^^^^^^^^^^

debug(develop:0xe57f3608...)>

```
这个界面总是是逐步调试的，可以看到上面的Commands说明里就是这个debug界面的操作方法，比如enter进入调试的下一步，v可以显示当前步骤所有变量的值，还有断点之类的，就不具体说了。


### 编写测试脚本
然后我在`test/`目录下新建一个`tinycoin.js`的测试脚本，这里可以使用nodejs传统的mocha测试模块和chai断言库，但是truffle在mocha上又增加了一层，方便针对智能合约的调试。我们看一下这个文件：

```
var TinyCoin = artifacts.require("./TinyCoin.sol")

contract("TinyCoin", function(accounts) {
	it("should put 10000 tinycoin in the first account", function(){
		return TinyCoin.deployed().then(function(instance) {
			return instance.balanceOf.call(accounts[0]);
		})
		.then(balance=>{
			balance = web3.fromWei(balance)
			assert.equal(	balance, 10000)
		})
	})
	it(`should succeed in a transfer
      * status should be 0x1
      * should have a Transfer event with correct output` , ()=>{
		return TinyCoin.deployed().then( instance=>{
			return instance.transfer(accounts[1],web3.toWei(10),{from:accounts[0]})
		})
		.then(ret=>{
			assert.equal(ret.receipt.status, "0x1", `expect status to be 0x1`)
			var eventTransfer = null
			for (var i = 0; i < ret.logs.length; i++) {
    		var log = ret.logs[i];
				if(log.event == "Transfer")
							eventTransfer = log
			}
			//should have new event
			assert(eventTransfer != null, 'except event to be Transfer')
			//should have correct to,from and value
			assert.equal(eventTransfer.args.from, accounts[0], `expect from to be account0`)
			assert.equal(eventTransfer.args.to, accounts[1], `expect to to be account1`)
			assert.equal(eventTransfer.args.value, web3.toWei(10), `expect value to be 1000`)
		})
	})
})

```
* `var TinyCoin = artifacts.require("./TinyCoin.sol")`
哪里都通用，TinyCoin中包含了合约的所有信息（但不是合约实例，合约实例在deployed中）
* `contract("TinyCoin", ...`
mocha的describe在这里被替换成contract， 并且传进来所有帐号`accounts`方便我们调用合约方法。
* 下面其中是一个测试样例，用以确认发起合约的帐号中确实存在 10000个 tinyCoin。方法中调用了ERC20的call方法`balanceOf`，来查询`accounts[0]`也就是发起合约的帐号中有多少tinycoin的余额。然后用`assert`验证余额是否和期望值一样。
```
it("should put 10000 tinycoin in the first account", function(){
    return TinyCoin.deployed().then(function(instance) {
        return instance.balanceOf.call(accounts[0]);
    })
    .then(balance=>{
        balance = web3.fromWei(balance)
        assert.equal(    balance, 10000)
    })
})
```
* 另一个测试样例中发起了一个token交易，帐号0向帐号1发送了10个tinycoin。在合约中，发起转账会出发一个事件，叫Transfer，Transfer中有三个数据：发送方/接收方/交易数量，因此这里我们还需要验证是否抛出了Transfer以及Transfer中的数据是否正确：
```
for (var i = 0; i < ret.logs.length; i++) {
  var log = ret.logs[i];
  if(log.event == "Transfer")
        eventTransfer = log
}
```

完成测试脚本以后，我们还是转到刚才的`truffle develop`的命令行界面，输入`test`即可开始运行测试脚本，会输出测试结果：
```
truffle(develop)> test
Using network 'develop'.



  Contract: TinyCoin
    ✓ should put 10000 tinycoin in the first account
    ✓ should succeed in a transfer
      * status should be 0x1
      * should have a Transfer event with correct output (41ms)


  2 passing (83ms)

```

## 总结
本篇主要介绍了truffle的核心功能的基本操作，包括编写合约、迁移脚本、搭建测试环境、打包、调试和测试脚本。更多具体功能请参照官网文档，或关注这里的更新

## 相关链接
* truffle official website https://truffleframework.com/
* truffle docs https://truffleframework.com/docs
* truffle tutorials https://truffleframework.com/tutorials
* web3js docs https://web3js.readthedocs.io/en/1.0/
